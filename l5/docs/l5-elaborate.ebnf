valid_program := () | top_level...

top_level := 
public_regulative_rule
    | private_regulative_rule
    | constitutive_definition
    | type_definition
    | type_instancing
    | comments

# Top level targets
public_regulative_rule := "$" regulative_rule
private_regulative_rule := "*" regulative_rule

regulative_rule := 
    # Name of rule
    regulative_label 

    # Entities the rule acts on
    "::" regulative_typed_arg_decl ("," regulative_typed_arg_decl)...

    # A regulative rule must have either a 
    # regulative constraint, deontic temporal action, or both

    # When to apply the regulative rule
    [regulative_constraint]

    # Permissions/Obligations granted
    [deontic_temporal_action]

    # Regulative rule conclusions
    [regulative_rule_conclusions]...

constitutive_definition :=
    "DEFINE" constitutive_label "::" typed_arg_decl ("," typed_arg_decl )
        "->" expression

constitutive_definition_invocation := constitutive_label "(" expression ("," expression) ")"

regulative_constraint := 
    "WHEN" bool_valued_expression

deontic_temporal_action :=
    ["ALWAYS"] ("PERMITTED" | "OBLIGATED") action_expr [temporal_constraint] [instance_tag]

regulative_rule_conclusions := 
    regulative_rule_conclusion_case 
    (mutation)...
    (regulative_rule_invocation | deontic_temporal_action)...

regulative_rule_conclusion_case := 
    # If only the deontic action is present
    ( "IF" ["NOT"] "PERFORMED" )
    # If only the constraint is present
    | ( "IF" ["NOT"] "FULFILLED" )
    # If both the constraint and deontic action is present
    | ( "IF" ["NOT"] "FULFILLED" "AND" ["NOT"] "PERFORMED" )
regulative_rule_invocation := regulative_label "(" expression ("," expression) ")"

# "{Jules} pays {`Ze Ming`}" = 500
mutation := relational_identifier "=" (expression  | "?")

action_expr := expression
# action_duration := ("UNTIL" absolute_time_declaration) | ("FOR" relative_time_declaration)
instance_tag := "BLAME" expression | ( "[" expression [("," expression)...] "]" )

temporal_constraint := 
    ("WITHIN" | "BETWEEN" | "BEFORE" | "BEFORE_ON" | "AFTER" | "AFTER_ON" | "ON")
    (aboslute_time_declaration | relative_time_declaration | matching_time_declaration)

# 03/20/2013
absolute_time_declaration := numeric_date "/" numeric_month "/" numeric_year
relative_time_declaration := @regex("\d+") @further_specification
matching_time_declaration := @further_specification

regulative_typed_arg_decl := (arg_label [("|" arg_label)...]) ":" type_label
typed_arg_decl := arg_label ":" type_label

type_definition := "TYPE" type_label 

# t.walk && (drink(t) || t.eat)
expression := 
    | "(" expression ")"
    | conditional_expr
    | unaryop_expr
    | binaryop_expr
    | constitutive_definition_invocation
    | term 

conditional_expr := bool_valued_expression "?" expression ":" expression
unaryop_expr := ("!" | "-") expression
binaryop_expr := 
    int_valued_expression ("-" | "+" | "*" | "/") int_valued_expression
    | bool_valued_expression ("AND" | "OR") bool_valued_expression

term := 
    literal
    | relational_identifier

# Type-checked expressions
bool_valued_expression := expression 
int_valued_expression := expression

# Instancing syntax
type_instancing := "DECLARE" (var_label ":" type_label) | relational_identifier

regulative_label := regular_identifer
constitutive_label := regular_identifer
arg_label := regular_identifer
var_label := regular_identifer
type_label := literal_type | regular_identifer

literal_type := "bool" | "int" | "float"
literal := primitive_literal
primitive_literal := @regex("\d+") | "True" | "False"

# Define this because it's used everywhere
regular_identifer := identifier | quoted_identifier

# walk
identifier := @regex("[a-zA-Z_][a-zA-Z0-9_]*")

# Backtick-quoted string, escapes `
# `consumes alcohol`
quoted_identifier := @further_specification

# Doublequotes-quoted string, escapes ", {, }
# "{person.child} consumes alcohol"
# Forces evaluation of the expressions within
# and always evaluates to a symbolic boolean
relational_identifier := @further_specification

numeric_date := @further_specification
numeric_month := @further_specification
numeric_year := @further_specification

comments := "--" @regex("[^\n]*")
